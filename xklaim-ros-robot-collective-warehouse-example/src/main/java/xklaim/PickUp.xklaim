package xklaim.arm

import klava.Locality
import messages.ModelState
import messages.XklaimToRosConnection
import ros.Publisher

proc PickUp(String rosbridgeWebsocketURI,Locality DeliveryRobot,Double x, Double y) {
	var poseX = x
	var poseY = y

	if (x > 0) {
		poseX = poseX + 0.3
	} else {
		poseX = poseX - 0.3
	}
	if (y < 0) {
		poseY = poseY + 0.5
	} else {
		poseY = poseY - 0.5
	}

	while (true) {

		in("itemDelivered",var String id,x,y)@DeliveryRobot

		println("###### Item " + id + " delivered at (" + x + "," + y + ")")

		// wait for a short delay before picking up the item
		Thread.sleep(2000)

		// connect to the ROS bridge
		val bridge = new XklaimToRosConnection(rosbridgeWebsocketURI)

		// pick up the item and pose it in the destination area
		val Pose_item = new Publisher("/gazebo/set_model_state", "gazebo_msgs/ModelState", bridge)
		val modelstate = new ModelState()

		modelstate.pose.position.x = poseX
		modelstate.pose.position.y = poseY

		modelstate.model_name = id
		modelstate.reference_frame = "world"
		Pose_item.publish(modelstate)

		println("###### Item " + id + " posed at (" + poseX + "," + poseY + ")")

		if (y < 0) {
			poseY = poseY + 0.3
		} else {
			poseY = poseY - 0.3
		}
	}
}
