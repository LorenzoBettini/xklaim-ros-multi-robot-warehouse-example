package xklaim.arm

import messages.JointTrajectory
import messages.XklaimToRosConnection
import ros.Publisher
import ros.SubscriptionRequestMsg

proc Lay(String rosbridgeWebsocketURI) {
	val local = self

	// wait for the completion of the rotation 
	in("rotationCompleted")@self

	// wait for the arrival of the delivery robot
	in("ready")@self

	// connect to the ROS bridge
	val bridge = new XklaimToRosConnection(rosbridgeWebsocketURI)

	// Initialize a publisher for the topic related to the control of the robot's arm
	val pub = new Publisher("/arm_controller/command", "trajectory_msgs/JointTrajectory", bridge);

	// set joint positions for performing the lay movement 
	val jointPositions = #[-0.9546, -0.0097, -0.9513, 3.1400, 1.7749, -0.0142]
	val JointTrajectory layTrajectory = new JointTrajectory().positions(jointPositions).jointNames(#[
		"joint1",
		"joint2",
		"joint3",
		"joint4",
		"joint5",
		"joint6"
	]);

	// publish the trajectory to lay the arm
	pub.publish(layTrajectory)

	// subscribe to the topic providing the actual status of the arm
	bridge.subscribe(
		SubscriptionRequestMsg.generate("/arm_controller/state").setType("control_msgs/JointTrajectoryControllerState").
			setThrottleRate(1).setQueueLength(1),
		[ data, stringRep |
			// extract the actual joint positions from the arm's status
			val actual = data.get("msg").get("actual").get("positions")

			// calculate the delta between the actual joint positions and the destination positions
			// to measure the completeness of the arm movement
			var delta = 0.0
			val tolerance = 0.002
			for (var i = 0; i < 6; i = i + 1) {
				delta += Math.pow(actual.get(i).asDouble() - jointPositions.get(i), 2.0)
			}
			val norm = Math.sqrt(delta)

			if (norm <= tolerance) { /* the arm has completed the lay movement */
				// activate the release process
				out("layCompleted")@local

				// unsubscribe from the arm state topic
				bridge.unsubscribe("/arm_controller/state")
			}
		]
	)
}
